# -*- mode: qore; indent-tabs-mode: nil -*-

# service: issue-3022
# serviceversion: 1.0
# servicedesc: issue 3022 test service
# parse-options: PO_NEW_STYLE, PO_REQUIRE_TYPES, PO_STRICT_ARGS
# serviceauthor: Qore Technologies, s.r.o.
# autostart: false
# remote: false
# ENDSERVICE

# desc: init
# name: init
%requires HttpServerUtil

public class Issue3022Authenticator inherits public HttpServer::AbstractAuthenticator {
    bool requiresAuthentication() {
        return True;
    }

    string getRealm() {
        return "issue3022";
    }

    *hash<HttpResponseInfo> authenticateRequest(HttpListenerInterface listener, hash hdr, reference<hash> cx) {
        if (!hdr.hasKey("x-api-key"))
            return cast<hash<HttpResponseInfo>>(RestHandler::makeResponse(401, "unauthorized missingkey"));
        if (hdr."x-api-key" != "abc12345")
            return cast<hash<HttpResponseInfo>>(RestHandler::makeResponse(401, "unauthorized invalidkey"));
        if (hdr."x-api-auth" != "loremipsum")
            return cast<hash<HttpResponseInfo>>(RestHandler::makeResponse(403, "xxforbidden"));

        return NOTHING;
    }
}

class ARestClass inherits AbstractRestClass {
    constructor() : AbstractRestClass() {}

    string name() {
        return "a";
    }

    hash<auto> get(hash<auto> cx, *hash<auto> ah) {
        return RestHandler::makeResponse(200, "okok");
    }
}

class Issue3022RestHandler inherits AbstractServiceRestHandler {
    constructor() : AbstractServiceRestHandler("/issue3022", False, new Issue3022Authenticator()) {
        addClass(new ARestClass());
        addListener(30220);
    }

    removeRootPath(reference<string> path) {
        path =~ s/^\/*issue3022//;
    }
}

sub init() {
    Issue3022RestHandler rh();
    ServiceApi::bindHttp(rh);
}
# END
